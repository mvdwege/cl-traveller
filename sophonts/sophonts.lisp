;;;; Sophont generation and management.

;;; We could just implement it using a sophont base class and using
;;; the defsophont macro to define new subclasses. However, the MOP
;;; gives us the tools to store sophont racial data in the class
;;; definition out of sight of the instances, and to add behaviour to
;;; that data. So see this as an experiment in using AMOP.
(in-package :traveller)

;;; Sophont class metaobject
(defclass sophont-class (standard-class) 
  ((characteristics :initarg :characteristics
		    :reader characteristics)
   (characteristic-dice :initarg :characteristic-dice
			:reader characteristic-dice)
   (homeworld :initarg :homeworld 
	      :reader homeworld)
   (native-terrain :initarg :native-terrain
		   :reader native-terrain
		   :reader native-terrain-mod)
   (locomotion :initarg :locomotion
	       :reader locomotion)
   (ecological-niche :initarg :ecological-niche
		     :reader ecological-niche)))

;; Keep the compiler happy. Especially SBCL insists on this method
;; existing. We're not doing really deep MOP things, so a simple "Yes,
;; this is supposed to be a subclass of standard-class" will suffice.
(defmethod closer-mop:validate-superclass ((class sophont-class) (super standard-class)) 
  t)

;;; Homeworld generation
(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'homeworld)))
  (setf (slot-value sophont slot) (make-world)))

;;; Determine Native Terrain, Locomotion, and Ecological Niche.
(defvar *terrain-types* 
  '(mountain desert exotic rough-wood rough clear forest wetlands
  wetland-woods ocean ocean-depths))

(defvar *locomotion-types*
  '((walker walker amphib amphib amphib walker walker amphib amphib flyphib Aquatic)
    (walker walker walker walker walker walker walker aquatic walker swim diver)
    (walker walker walker walker walker walker walker walker walker swim diver)
    (walker walker walker walker walker walker walker walker walker swim diver)
    (walker walker flyphib walker walker walker walker triphib triphib aquatic diver)
    (flyer flyer flyer flyer flyer walker walker flyer flyphib diver diver)))

(defvar *basic-niche*
  '(producer producer herbivore herbivore omnivore omnivore omnivore
    omnivore omnivore carnivore carnivore scavenger scavenger))

(defvar *ecological-niche*
  '(herbivore (grazer grazer grazer intermittent intermittent
	       intermittent intermittent grazer grazer grazer grazer
	       grazer filter)
    omnivore (hunter hunter hunter hunter hunter gatherer
	      hunter-gatherer gatherer gatherer gatherer gatherer
	      gatherer eater)
    carnivore (pouncer pouncer pouncer pouncer pouncer pouncer chaser
	       chaser chaser chaser trapper siren killer)
    scavenger (carrion-eater carrion-eatercarrion-eater hijacker
	       hijacker hijacker intimidator intimidator intimidator
	       intimidator intimidator reducer reducer)
    producer (collector collector collector collector collector
	      collector basker basker basker basker basker basker)))

;; Only four different conditions, so just a few ifs will do, no need
;; to break out method dispatch.
(defmethod terrain-modifiers ((sophont sophont-class))
  (+
   (if (>= (atmosphere (homeworld sophont)) 8)
       -2
       0)
   (if (<= (size (homeworld sophont)) 5)
       -1
       0)
   (if (>= (hydrographics (homeworld sophont)) 6)
       1
       0)
   (if (>= (hydrographics (homeworld sophont)) 9)
       1
       0)))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'native-terrain)))
  (setf (slot-value sophont slot) 
	(min 5 (max -5 (+ (flux) (terrain-modifiers sophont))))))


(defmethod native-terrain :around ((sophont-class sophont-class)) 
  (string-capitalize
   (substitute #\Space #\-
	       (symbol-name
		(nth (+ (call-next-method) 5) *terrain-types*)))))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'locomotion)))
  (setf (slot-value sophont slot) 
	(nth (min 10 (max 0 (+ (flux) 5 (native-terrain-mod sophont)))) 
	     (nth (random 6) *locomotion-types*))))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'ecological-niche)))
  (let ((basic-niche (nth (+ (flux) 6) *basic-niche*)))
    (setf (slot-value sophont slot) 
	  (list basic-niche 
		(flux-on  
		 (getf *ecological-niche* basic-niche)
		 :shift 6
		 :dm (native-terrain-mod sophont))))))

;;; Generate Characteristics
(defvar *sophont-characteristics*
  `(,(loop repeat 11 collect 'strength)
     (agility agility agility agility dexterity dexterity dexterity
     grace grace grace grace)
     (stamina stamina stamina stamina endurance endurance endurance vigor vigor vigor vigor)
     ,(loop repeat 11 collect 'intelligence)
     (instinct instinct instinct instinct education education
     education training training training training)
     (caste caste caste social-standing social-standing
     social-standing social-standing charisma charisma charisma
     charisma)))

(defvar *characteristic-values*
  '((1 1 2 2 2 2 3 4 5 6 7 8)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 2 2 2 2 2)
    (1 1 2 2 2 2 2 2 2 2 2 2)))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'characteristics)))
  (let ((dm (cond
	      ((eql (locomotion sophont) 'flyer) -2)
	      ((eql (locomotion sophont) 'swimmer) 2)
	      ((eql (locomotion sophont) 'diver) 2)
	      (t 0)))
	(characteristics))
    (dotimes (c 6) (push 
		    (nth (min 10 (max 0 (+ (flux) 5 dm)))
			 (nth c *sophont-characteristics*)) characteristics))
    (setf (slot-value sophont slot) (nreverse characteristics))))
    
(defmethod genetic-profile ((sophont sophont-class))
  (let ((clist (characteristics sophont))
	(profile-list))
    (dolist (c clist)
      (push (subseq (symbol-name c) 0 1) profile-list))
    ;; The last characteristic has two posibilities starting with a
    ;; 'C'. If it is Caste, then the genetic profile letter (currently
    ;; the first element of profile-liest) must be 'K' to distinguish
    ;; it from Charisma.
    (if (eql (car (reverse clist)) 'caste)
	(setf (car profile-list) "K"))
    (format nil "狺铗弼弪箦痱镦殪瀛扉篝┅┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢с栳蜥泗弪轶糸悱溟沐┅戾è蝈篚祠扉篝┅ㄤ雉轫弩ㄣ订戾舄è汨狎徙翦蜷篝殂铘ㄣ栳蜥泗弪轶糸泱箫痂镱舂┅ㄣ霏泔祯眍铘汨狎徙翦蜷篝殂鲠祯弩┅ㄤǐㄩ冀博钺糸鲥翦蝌衢瞽盹箫痂镱舂癌ㄩㄦ轭с栳箦ㄥ泔祜玳汜飙铋汨箫痂镱舂癌ㄩㄦ轭ю秕钽弪ㄥ泔祜玳汜飙铋汨箫痂镱舂癌┅蝈篚祠ㄩ矧ㄥ耢汨狎徙翦蜷篝殂у漉汜糸镱ㄥ耢汨狎徙翦蜷篝殂趄衢铋铉┅ㄦ祯镱泠泔祯眍轰漤┅┅瘐箬蝈篚祠蝈篚祠扉篝┅箦翩箪雉鲠祯箫痂镱箪雉铗弼弪箦蝈篚祠扉篝┅┅换箩箦沆狍骘箫痂镱轭溟鲩漉犰ㄤ彐沆狍箫痂镱īè钺礤横沣弩箫钺礤洪铋翎蜱侯犴濠ㄨ镯鬻矧熹横沣弩箫栾礤黠蜢洪铋翎蜱鸿镯鬻矧熹ㄢ轵翳黠蜢候遽溴忾螋梓矧熹ㄡ珏横沣弩箫徵洪铋翎蜱横珏ㄣ栳蜥泗弪轶糸泱横沣弩箫汨狎徙翦蜷篝殂洪铋翎蜱恒栳蜥泗弪轶糸泱┅ê礤翎沆狍箫痂镱舡沆狍螬换蔑铞孱殄钽磲泸锂ㄤ彐磲泸溴骟镳栾铘钺礤啜溴驺灬篌钺礤箫痂镱舂īê礤翎沆狍箫痂镱舡沆狍螬┅换深溟鲩漉犰描狎徙翦蜷篝殂ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌ㄩ钿轹殇踽箫痂镱舂箪雉ㄥ耢с栳蜥泗弪轶糸泱┅戾è蝈篚祠扉篝┅ㄤ雉轫弩ㄣ订戾è汨狎徙翦蜷篝殂鲠祯癌ㄤ殂铘ㄣ栳蜥泗弪轶糸悱溟沐ㄣ灬篌镦轭溟鲩漉犰┅┅ㄩ窘溟沐穿痱镧ō溟沐博箦翩汨狎徙翦蜷篝殂鲠祯辈┅ㄩ钽汨狎徙翦蜷篝殂鲠祯蝻祆溟沐┅瘐箬汨狎徙翦蜷篝殂鲠祯蝈篚祠扉篝┅箦翩箪雉鲠祯轭溟鲩漉犰箪雉铗弼弪箦蝈篚祠扉篝┅┅