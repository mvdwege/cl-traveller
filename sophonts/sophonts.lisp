;;;; Sophont generation and management.

;;; We could just implement it using a sophont base class and using
;;; the defsophont macro to define new subclasses. However, the MOP
;;; gives us the tools to store sophont racial data in the class
;;; definition out of sight of the instances, and to add behaviour to
;;; that data. So see this as an experiment in using AMOP.
(in-package :traveller)

;;; Sophont class metaobject
(defclass sophont-class (standard-class) 
  ((characteristics :initarg :characteristics
		    :initarg :genetic-profile
		    :reader characteristics)
   (characteristic-dice :initarg :characteristic-dice
			:reader characteristic-dice)
   (homeworld :initarg :homeworld 
	      :reader homeworld)
   (native-terrain :initarg :native-terrain
		   :reader native-terrain
		   :reader native-terrain-mod)
   (locomotion :initarg :locomotion
	       :reader locomotion)
   (ecological-niche :initarg :ecological-niche
		     :reader ecological-niche)
   (caste-structure :initarg :caste-structure
		    :reader caste-structure)
   (caste-table :initarg caste-table
		:reader caste-table)
   (gender-structure :initarg :gender-structure
		     :reader gender-structure)
   (gender-table :initarg :gender-table
		 :reader gender-table)
   (gender-differences :initarg :gender-differences
		       :reader gender-differences)))

;; Keep the compiler happy. Especially SBCL insists on this method
;; existing. We're not doing really deep MOP things, so a simple "Yes,
;; this is supposed to be a subclass of standard-class" will suffice.
(defmethod closer-mop:validate-superclass ((class sophont-class) (super standard-class)) 
  t)

;;; Homeworld generation
(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'homeworld)))
  (setf (slot-value sophont slot)
	;; Brute-force approach: generate worlds until we have one
	;; suitable for life.
	(do ((world (make-world) (make-world)))
	    ((and
	      (>= (population world) 7)
	(>= (atmosphere world) 2)) world)
	  ())))
     
;;; Determine Native Terrain, Locomotion, and Ecological Niche.
(defvar *terrain-types* 
  '(mountain desert exotic rough-wood rough clear forest wetlands
  wetland-woods ocean ocean-depths))

(defvar *locomotion-types*
  '((walker walker amphib amphib amphib walker walker amphib amphib flyphib aquatic)
    (walker walker walker walker walker walker walker aquatic walker swim diver)
    (walker walker walker walker walker walker walker walker walker swim diver)
    (walker walker walker walker walker walker walker walker walker swim diver)
    (walker walker flyphib walker walker walker walker triphib triphib aquatic diver)
    (flyer flyer flyer flyer flyer walker walker flyer flyphib diver diver)))

(defvar *basic-niche*
  '(producer producer herbivore herbivore omnivore omnivore omnivore
    omnivore omnivore carnivore carnivore scavenger scavenger))

(defvar *ecological-niche*
  '(herbivore (grazer grazer grazer intermittent intermittent
	       intermittent intermittent grazer grazer grazer grazer
	       grazer filter)
    omnivore (hunter hunter hunter hunter hunter gatherer
	      hunter-gatherer gatherer gatherer gatherer gatherer
	      gatherer eater)
    carnivore (pouncer pouncer pouncer pouncer pouncer pouncer chaser
	       chaser chaser chaser trapper siren killer)
    scavenger (carrion-eater carrion-eater carrion-eater hijacker
	       hijacker hijacker intimidator intimidator intimidator
	       intimidator intimidator reducer reducer)
    producer (collector collector collector collector collector
	      collector basker basker basker basker basker basker)))

;; Only four different conditions, so just a few ifs will do, no need
;; to break out method dispatch.
(defmethod terrain-modifiers ((sophont sophont-class))
  (+
   (if (>= (atmosphere (homeworld sophont)) 8)
       -2
       0)
   (if (<= (size (homeworld sophont)) 5)
       -1
       0)
   (if (>= (hydrographics (homeworld sophont)) 6)
       1
       0)
   (if (>= (hydrographics (homeworld sophont)) 9)
       1
       0)))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'native-terrain)))
  (setf (slot-value sophont slot) 
	(min 5 (max -5 (+ (flux) (terrain-modifiers sophont))))))

(defmethod native-terrain :around ((sophont-class sophont-class)) 
  (string-capitalize
   (symbol-to-name (nth (+ (call-next-method) 5) *terrain-types*))))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'locomotion)))
  (setf (slot-value sophont slot) 
	(nth (+ 5 (native-terrain-mod sophont))(nth (random 6) *locomotion-types*))))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'ecological-niche)))
  (let ((basic-niche (flux-on *basic-niche* :shift 6)))
    (if (eq basic-niche 'producer)
	(setf (slot-value sophont 'locomotion) 'immobile))
    (setf (slot-value sophont slot) 
	  (list basic-niche 
		(flux-on  
		 (getf *ecological-niche* basic-niche)
		 :shift 6
		 :dm (native-terrain-mod sophont))))))

;;; Generate Characteristics
(defvar *sophont-characteristics*
  `(,(loop repeat 11 collect 'strength)
     (agility agility agility agility dexterity dexterity dexterity
     grace grace grace grace)
     (stamina stamina stamina stamina endurance endurance endurance vigor vigor vigor vigor)
     ,(loop repeat 11 collect 'intelligence)
     (instinct instinct instinct instinct education education
     education training training training training)
     (caste caste caste social-standing social-standing
     social-standing social-standing charisma charisma charisma
     charisma)))

;;; Number of dice to roll for characteristic generation
(defvar *characteristic-values*
  '((1 1 2 2 2 2 3 4 5 6 7 8)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 3 3 3 3 3)
    (1 1 2 2 2 2 2 2 2 2 2 2)
    (1 1 2 2 2 2 2 2 2 2 2 2)))

(defmethod slot-unbound (class (sophont sophont-class) (slot (eql 'characteristics)))
  (let ((dm (cond
	      ((eql (locomotion sophont) 'flyer) -2)
	      ((eql (locomotion sophont) 'swimmer) 2)
	      ((eql (locomotion sophont) 'diver) 2)
	      (t 0)))
	(characteristics))
    (dotimes (c 6) (push 
		    (flux-on (nth c *sophont-characteristics*) :dm dm) characteristics))
    (setf (slot-value sophont slot) (nreverse characteristics))))

;; If characteristics is set using :genetic-profile, we have to
;; convert it to a list. If not, it has either been explicitly set as
;; a list in the initargs, or we will trigger generation through
;; slot-unbound.
(defmethod characteristics :around ((sophont sophont-class))
  (let ((characteristics (slot-value sophont 'characteristics)))
    (when (stringp characteristics)
      (let ((characteristics-list)
	    (characteristics-grouped (mapcar #'remove-duplicates *sophont-characteristics*)))
	;; Change spelling of Caste to Kaste to make it match with the
	;; "K" in the GP string.
	(nsubstitute 'kaste 'caste (nth 5 characteristics-grouped))
	(dotimes (c 6)
	  (push
	   (find (aref characteristics c) 
		 (nth c characteristics-grouped)
		 :test #'(lambda (x y) 
			   (eql x (aref (string-upcase (symbol-name y)) 0))))
	   characteristics-list))
	;; Revert 'Kaste' spelling back to normal.
	(nsubstitute 'caste 'kaste characteristics-list)
	(setf (slot-value sophont 'characteristics) (nreverse characteristics-list)))))
  (call-next-method))

(defmethod genetic-profile ((sophont sophont-class))
  (let ((clist (characteristics sophont))
	(profile-list))
    (dolist (c clist)
      (push (subseq (symbol-name c) 0 1) profile-list))
    ;; The last characteristic has two posibilities starting with a
    ;; 'C'. If it is Caste, then the genetic profile letter (currently
    ;; the first element of profile-list) must be 'K' to distinguish
    ;; it from Charisma.
    (if (eql (car (reverse clist)) 'caste)
	(setf (car profile-list) "K"))
    (format nil "狺铗弼弪箦痱镦殪瀛扉篝┅┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢с栳蜥泗弪轶糸悱溟沐┅戾è蝈篚祠扉篝┅ㄤ雉轫弩ㄣ订戾舄è汨狎徙翦蜷篝殂铘ㄣ栳蜥泗弪轶糸泱箫痂镱舂┅ㄣ霏泔祯眍铘汨狎徙翦蜷篝殂鲠祯弩┅ㄤǐㄩ冀博钺糸鲥翦蝌衢瞽盹箫痂镱舂癌ㄩㄦ轭с栳箦ㄥ泔祜玳汜飙铋汨箫痂镱舂癌ㄩㄦ轭ю秕钽弪ㄥ泔祜玳汜飙铋汨箫痂镱舂癌┅蝈篚祠ㄩ矧ㄥ耢汨狎徙翦蜷篝殂у漉汜糸镱ㄥ耢汨狎徙翦蜷篝殂趄衢铋铉┅ㄦ祯镱泠泔祯眍轰漤┅┅瘐箬蝈篚祠蝈篚祠扉篝┅箦翩箪雉鲠祯箫痂镱箪雉铗弼弪箦蝈篚祠扉篝┅┅换清钿弪珏铄蜥糸镱ㄤ彐鲠珏钿弪篝蝓泗躜瀛翎忪濯Ж箫扉翎轵箫扉翎轵遽遽漉犰漉犰漉犰骓骓珧秕珧秕皓换歪痧轭骝镯铛礅弪邃珏钿弪麸钺礤ㄤ彐鲠珏钿弪篝蝓泗躜弩Ж箫扉翎轵箫祜漉犰ㄦ屙犰磲戾骓ㄦ屙犰磲戾铄豸弪遽ㄥ珑滹铒徙糸鲠麸忮狎弪珧秕镱赭翳蝈骘躜骈鲥箝┅ㄤ彐鲠珏钿弪翎忪弩啜箫扉翎轵ì括祜镳蝈疱狒北泔祆邈箫祜┅漉犰ㄦ屙犰驽磲戾驽磲戾驽磲戾磲戾驽磲戾磲戾磲戾磲戾驽磲戾磲戾遽ㄥ珑滹铒彗绛滹铒彗绛滹铒徙糸鲠麸彗绛滹铒徙糸鲠麸忮狎弪忮狎弪忮狎弪徙糸鲠麸忮狎弪骓ㄦ屙犰驽磲戾驽磲戾磲戾驽磲戾磲戾铄豸弪铄豸弪铄豸弪磲戾铄豸弪珧秕箝箝骘躜骘躜赭镱翳蝈骈鲥骈鲥箝箝┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢х孱溴颦篝蝓泗躜濠┅箦翩箪雉鲠祯箫痂镱箪雉ㄦ祯镱珏钿弪篝蝓泗躜瀛翎忪濯┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢х孱溴颦翎忪濠┅换深轸獒珏钿弪狍箝珙礤铘鏖翳箪雉ㄧ孱溴颦篝蝓泗躜濠箫痂镱箦翩箪雉鲠祯箫痂镱箪雉戾è珏钿弪翎忪濠躅戾篌ㄥ耢珏钿弪篝蝓泗躜х蝻躔磲疸狎＇灬礅溽ㄧ孱溴颟瘐箬珏钿弪珏钿弪翎忪濠ㄧ弭珏钿弪篝蝓泗躜弩珏钿弪篝蝓泗躜濠┅ㄤīè戾铉翳珏钿弪翎忪濠北铗弼弪箦珏钿弪翎忪濠瘐箬ㄦ祯镱ㄧ弭珏钿弪翎忪弩珏钿弪篝蝓泗躜濠珏钿弪翎忪濠┅┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢х孱溴颦溟骀弪孱沐螬┅戾è溟骀弪孱沐翎忪铋飑ㄤ镬轶ㄧ蝈盹鲥漉痨殂狒弩ㄧ孱溴颦翎忪箫痂镱舂┅戾è溟骀弪孱沐铋飑ㄤ雉轫弩ㄣ订戾è骒貘蝻祆ㄦ祯┅瘐箬换羽邈獒趄遽繇孱骘郁蝈铉翳犷蝻祆忮赭邋犷碑ㄣ镱è犷癌窘骒貘蝻祆畅蝻祆骒貘蝻祆┅è犷癌骒貘蝻祆暴暴è犷窘骒貘蝻祆暴冀骒貘蝻祆暴癌骒貘蝻祆┅溟骀弪孱沐螬┅箦翩ㄧ弭溟骀弪孱沐翎忪绌铗弼弪箦溟骀弪孱沐螬┅箦翩箪雉鲠祯箫痂镱箪雉溟骀弪孱沐翎忪濠┅换冕篝珏铄蜥糸镱ㄤ彐鲠汜篝瀛篝蝓泗躜瀛翎忪濯Ж怙澌邈镱镯殂驷黹禊黹扉翎蝙箫汩犰箅殪戾洎ㄤ彐鲠汜篝瀛翎忪弩Ж怙澌ㄨ遽戾珏钿弪犷糸怙澌箦铙矧礤盹蝙眭筱戾眭筱戾眭筱戾鲲殂箴邈獒沆狩邈镱镯殂ㄩ铑秭狒矧珏钿弪珲狎蝈箦狎汨弪狎糸筢灬怙蝈泸徭趔磲沆弪磲钺珏箴邈獒孱趄屦蝈铄躜驷黹禊ㄨ遽戾珏钿弪溴驽钿弪汜蝈玳鲥汜蝈玳鲥怛遽澉轭铄怛遽澉轭铄怛遽澉轭铄躅沆箴邈獒戾徜弪黹扉翎蝙礤溟珏钿弪衢溴筱秕箴邈獒扉篝箫熹殄翦汨铋汩犷麽蝌轱戾徜弪箴邈獒篝徭姗箫汩犰ㄡ螋轶珏钿弪孱骘蜚弪潋镱狎糸篝躅轸躅轸躅轸疳趄镱箴邈獒孱翦螋衢铄颟箴邈獒ㄤ屙轭轫躞躞屐弩徜鲩箫颦轭篝蝓泗矧箬犴犷屮疱钿徕戾溴驽泗轹鲠祯徕戾徜鲩箫颢箴矧鲩沐戾徜弪┅ㄤ彐鲠汜篝瀛翎忪弩躅轳蹂螵Ж怙澌怛衢邈镱镯殂溟蝈泗矧驷黹禊狎汨镱黹扉翎蝙珏铄蜥箫汩犰蝓戾颟ㄤ彐鲠汜篝瀛箅殪祗Жè义泔铃蝻钺豸殂龄黹徜鲲汜翦箫躅漤轫殂徙雯ㄓ痖铄榴踽钺豸殂硫糸祆弪硫糸篝麻镬镧殂刘翳矧ㄓ孱箫蝮刘麸盹糸鲥馏趄镧狒矧洛犴蔑眇豸弪买镫弪零麸迈蝈狨泸徙抿徭趔磲蔑眇豸弪尿轹弪惋戾ㄆ禊弪箩趑戾尿弩尼钽弪拈痨镯狒砒痨矬轹弩湾溟悌ㄅ眇狒蓬玳铄弪腻箝珙弪砒雉殂黔尿轹球狯┅è旗狃疱旗蹰溟泱澎邈趄镱殂骑蝈铙殂虱尿轹歪翳ㄌ遽溴儒狯尊铙蓬玳铄弪体珑邃涕衢箫氏冤ㄔ蜥汶邃提躅汨弪球狯轸殂湾汨犷殂留桁弭则徜弪ㄐ殪雉歪珙弭殂蕊篝殪蓬向漕犷沐蚂徜弩淘俩令轫犰涕驽吁痧矧提铉踽珏协徐犷蔑躅箦祆矧俞殪ㄔ徙糸泱需雉镱殂王箝汩犷俞痧弪向糸祆弪予轲┅è怎蝌弭序镧蜥眄弪郁蜥翦琦禹犰抿徭崎玷糸铉绎麸颟ㄓ遽驷蝈屿蹒澡蝻麇铜尿轹郁遽祠象磲钽弪议溴颟ㄓ躜鲥吾鲠硫汨吾鲩玑糸镱吁蝣轹犰阻邋戾羽蜥蔑眄郁蝈弭鏖箦酗禊礤蝮则衢铄鱼蝈孱吁猢ㄔ遽汨弪藻犴篝弪羽徙邈蜥骠令轫犰郁鬻狎组铉ㄕ钺蝽邃轴沣吁轸郁狎箬轲物与殪阱蝻淄末┅ㄤ彐礤翳镤汜篝瀛è箫痂镱箫痂镱舡沆狍螬ㄥ耢铘ㄣ栳蜥泗弪轶糸泱箫痂镱舂с狍翦┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢с狍翦篝蝓泗躜濠┅箦翩箪雉鲠祯箫痂镱с狍翦篝蝓泗躜濠ㄩㄣ狍翦箫痂镱舂蝻祆镱汜篝瀛篝蝓泗躜瀛翎忪濯铋飑┅ㄤ彐礤翳镤箪雉躅怙躅ㄣ灬篌箫痂镱箫痂镱舡沆狍螬箪雉ㄥ耢с狍翦翎忪濠┅ㄩㄣ狍翦箫痂镱舂汜篝瀛翎忪澧箦翩箪雉鲠祯箫痂镱с狍翦翎忪濠铋飑┅换箫痂镱泔铙趄蹉麸ㄤ彐磲泸溴骟镳栾铘é蝈篝轭轸狎珞啜磲脲轭篝犷沐箫痂镱舡沆狍轰轵邈舡篚疱蜚灬篌弩扉篝ㄦ轭洵沆狍箫痂镱舂篱铋翎蜱螬换换郁犷溽蜾箫痂镱趔换弱磲铋糸箦翩ㄦ轭洵沆狍ц蹴犷ㄤ彐箫痂镱侯犴ц蹴犷虹孱弭殂痱镦殪⒂呐膳英恒栳蜥泗弪轶糸悱溟沐Ж博鸿镯鬻矧熹磲脲黠蜢乎黟⒘阜仿构蘑侯狒轹瀛翦蝌衢红镢镯雉轱麽祀弪哄泔祜玳汜飙铋汨Ж镯铋鲲蝈桴铘弪玑翳弪弪虹孱溴颦篝蝓泗躜т踽虹孱溴颦翎忪Ж驽磲戾磲戾驽磲戾磲戾驽磲戾磲戾驽磲戾磲戾驽磲戾磲戾驽磲戾虹孱溴颦溟骀弪孱沐Ж磲戾ò癌驽磲戾ò癌┅换馏灬箦翩ㄦ轭洵沆狍п箪犷ㄤ彐箫痂镱侯犴п箪犷虹孱弭殂痱镦殪⒂挠膳英恒栳蜥泗弪轶糸悱溟沐Ж博鸿镯鬻矧熹磲脲黠蜢乎黟⒘阜豆付泞侯狒轹瀛翦蝌衢红镢镯雉轱麽祀弪哄泔祜玳汜飙铋汨Ж汜蝾轹矧痫躅沐颟虹孱溴颦篝蝓泗躜т踽虹孱溴颦翎忪Ж磲戾磲戾磲戾磲戾驽磲戾驽磲戾驽磲戾驽磲戾驽磲戾驽磲戾驽磲戾虹孱溴颦溟骀弪孱沐Ж磲戾ú畅驽磲戾ò癌┅换帅腧邋换韵南帅腧邋栳鲥冕篝瀣怩汜篝珏铄蜥糸镱轶铒骈铋箬邃弭箦翩ㄦ轭洵沆狍щ腧邋ㄤ彐箫痂镱侯犴щ腧邋虹孱弭殂痱镦殪⒂呐膳芒恒栳蜥泗弪轶糸悱溟沐Ж博鸿镯鬻矧熹磲脲黠蜢乎黟⒙付沉俺脾侯狒轹瀛翦蝌衢红镢镯雉轱麽祀弪哄泔祜玳汜飙铋汨Ж桢蜮轹矧珧狷弪虹孱溴颦篝蝓泗躜仆虹孱溴颦翎忪Ж驽磲戾磲戾铄豸弪驽磲戾驽磲戾铄豸弪磲戾磲戾磲戾铄豸弪磲戾虹孱溴颦溟骀弪孱沐Ж磲戾ǔ癌驽磲戾ò癌铄豸弪ò癌┅